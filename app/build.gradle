import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
    id "org.ajoberstar.grgit" version "5.0.0-rc.3"
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    defaultConfig {
        applicationId "com.example.khughes.machewidget"
        compileSdk 34
        minSdkVersion 28
        targetSdkVersion 34
        versionCode 96
        versionName "2024.02.12-09"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
            arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        production {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
    }
    tasks.register('versionTxt') { task ->
        doLast {
            SimpleDateFormat sdf = new SimpleDateFormat("dd MMM yyyy HH:mm:ss z")
            sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
            new File(projectDir, "VERSION.txt").text =
                    """Version: ${defaultConfig.versionName}
Commit: ${grgit.head().abbreviatedId}
Build date: ${sdf.format(new Date())}
Application-name: com.example.khughes.machewidget
"""
        }
    }

    flavorDimensions = ["none"]
    productFlavors {
        playstore {
            dimension "none"
//            applicationIdSuffix '.playstore'
            applicationId 'org.khpylon.fswidget'
        }
        github {
            dimension "none"
        }
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            if (variant.flavorName == "github") {
                outputFileName = "app-release.apk"
            }
        }
    }
    sourceSets {
        playstore {
            java {
                exclude "**/UpdateActivity.java"
            }
        }
    }

    preBuild.dependsOn versionTxt

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
        resources.excludes.add("META-INF/*")
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    namespace 'com.example.khughes.machewidget'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.0'

    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.3'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.14.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-avro:2.14.2'
    implementation 'org.msgpack:jackson-dataformat-msgpack:0.9.3'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.3'
    implementation 'com.google.android.libraries.places:places:3.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.databinding:databinding-runtime:7.4.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.activity:activity-ktx:1.7.2'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.fragment:fragment-ktx:1.6.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.webkit:webkit:1.8.0-alpha01'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'org.commonmark:commonmark:0.21.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    // Room components
    implementation "androidx.room:room-ktx:2.5.2"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    // DO NOT change annotationProcessor to kapt; this will break Rooms with Kotlin
    kapt "android.arch.persistence.room:compiler:1.1.1"
    kapt "android.arch.persistence.room:testing:1.1.1"
    kapt 'androidx.room:room-compiler:2.5.0'

    implementation 'com.github.jozefchmelar:persistentcookiejar:1.0.1'
    implementation 'com.github.skydoves:colorpickerview:2.2.4'
    implementation 'com.jjoe64:graphview:4.2.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // Fix duplicate class errors
    // https://www.youtube.com/watch?v=RplK16_-6mU
    configurations.implementation {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
    }

    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt 'androidx.lifecycle:lifecycle-compiler:2.5.1'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
