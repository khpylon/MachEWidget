import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
    id "org.ajoberstar.grgit" version "1.7.2"
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.example.khughes.machewidget"
        minSdkVersion 28
        targetSdkVersion 33
        versionCode 58
        versionName "2023.01.24"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
            arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        production {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
    }
    task versionTxt { task ->
        doLast {
            SimpleDateFormat sdf = new SimpleDateFormat("dd MMM yyyy HH:mm:ss z")
            sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
            new File(projectDir, "VERSION.txt").text =
"""Version: ${defaultConfig.versionName}
Commit: ${grgit.head().abbreviatedId}
Build date: ${sdf.format(new Date())}
Application-name: com.example.khughes.machewidget
"""
        }
    }

    flavorDimensions "none"
    productFlavors {
        playstore {
            dimension "none"
//            applicationIdSuffix '.playstore'
            applicationId 'org.khpylon.fswidget'
        }
        github {
            dimension "none"
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.flavorName.equals("github")) {
                outputFileName = "app-release.apk"
            }
        }
    }
    sourceSets {
        playstore {
            java {
                exclude "**/UpdateActivity.java"
            }
        }
    }

    preBuild.dependsOn versionTxt

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
    buildFeatures {
        viewBinding true
    }
}

configurations {
    cleanedAnnotations
    implementation.exclude group: 'org.jetbrains' , module:'annotations'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'

    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.3'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.13.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-avro:2.13.0'
    implementation 'org.msgpack:jackson-dataformat-msgpack:0.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.3'
    implementation 'com.google.android.libraries.places:places:3.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.databinding:databinding-runtime:7.3.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.activity:activity:1.6.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.fragment:fragment:1.5.5'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.webkit:webkit:1.5.0-rc01'
    implementation 'androidx.core:core:1.9.0'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'org.commonmark:commonmark:0.18.2'
    implementation "io.reactivex.rxjava2:rxjava:2.2.7"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    // Room components
    implementation "androidx.room:room-ktx:2.5.0-rc01"
    kapt "androidx.room:room-compiler:2.5.0-rc01"
    androidTestImplementation "androidx.room:room-testing:2.5.0-rc01"

    implementation 'com.github.jozefchmelar:persistentcookiejar:1.0.1'
    implementation 'com.github.skydoves:colorpickerview:2.2.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'

    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.5.1'
    annotationProcessor 'androidx.room:room-compiler:2.5.0-rc01'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}
